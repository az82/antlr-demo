group 'de.az.antlr'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'antlr'


sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// Fix for having Antlr 2 dependency on the classpath
// See https://github.com/gradle/gradle/issues/820
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

generateGrammarSource {
    // The ANTLR plugin does not create the package directory
    outputDirectory = new File("${project.buildDir}/generated-src/antlr/main/de/az/antlr/demo/parser")
    // No option for setting the package
    arguments += [
            "-visitor",
            "-long-messages",
            "-package", "de.az.antlr.demo.parser"
    ]
}

dependencies {
    // Antlr
    def antlrVersion = "4.7"
    antlr  group: "org.antlr", name: "antlr4", version: antlrVersion
    compile group: "org.antlr", name: "antlr4-runtime", version: antlrVersion
}
